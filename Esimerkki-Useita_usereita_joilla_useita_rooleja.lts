/** Musteero
 *  Miten tehdä systeemi jossa useita geneerisiä käyttäjiä joilla voi olla olemassa useita
 *  eri käyttäjärooleja systeemissä (esim. yhdellä käyttäjällä sekä asiakas, omistaja että admin).
 */
range BOOL = 0..1

// Geneerinen käyttäjä joka on rekisteröitynyt sekä asiakkaana, omistajana että adminina.
// Käyttäjän pitää kirjautumisen jälkeen valita minkä roolin hän ottaa systeemissä.
NOT_LOGGED_USER(N=1, M=1, O=1) = NOT_LOGGED_USER[N][M][O],
NOT_LOGGED_USER[cust:BOOL][keeper:BOOL][admin:BOOL] =
          (when(cust==1 || keeper==1 || admin==1) login -> USER[cust][keeper][admin]
          |when(cust==0)           register_as_customer -> NOT_LOGGED_USER[1][keeper][admin]),
USER[cust:BOOL][keeper:BOOL][admin:BOOL] =
          (when(cust==1)   selectCustRole   -> de-selectCustRole   -> USER[cust][keeper][admin]
          |when(keeper==1) selectKeeperRole -> de-selectKeeperRole -> USER[cust][keeper][admin]
          |when(admin==1)  selectAdminRole  -> de-selectAdminRole  -> USER[cust][keeper][admin]
          |logout -> NOT_LOGGED_USER).

CUSTOMER       = (selectCustRole -> CUSTOMER_STUFF),
CUSTOMER_STUFF = (some_customer_action -> CUSTOMER_STUFF
                 |de-selectCustRole -> CUSTOMER).

SHOPKEEPER       = (selectKeeperRole -> SHOPKEEPER_STUFF),
SHOPKEEPER_STUFF = (some_shopkeeper_action -> SHOPKEEPER_STUFF
                   |de-selectKeeperRole -> SHOPKEEPER).

ADMIN       = (selectAdminRole -> ADMIN_STUFF),
ADMIN_STUFF = (some_admin_action -> ADMIN_STUFF
              |de-selectAdminRole -> ADMIN).

||USERS = (user_1:NOT_LOGGED_USER(1,1,1)
        || user_2:NOT_LOGGED_USER(1,1,1)
        || user_3:NOT_LOGGED_USER(0,0,0)// Kolme eri käyttäjäinstanssia
        || user_1::CUSTOMER || user_1::SHOPKEEPER || user_1::ADMIN // Kullakin userilla oma rooliprosessi
        || user_2::CUSTOMER || user_2::SHOPKEEPER || user_2::ADMIN
        || user_3::CUSTOMER || user_3::SHOPKEEPER || user_3::ADMIN).
