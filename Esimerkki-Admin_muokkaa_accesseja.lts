/** Musteero
 *  Miten tehdä systeemi jossa admin säätää muiden oikeuksia
 */
range BOOL = 0..1  // Onko oikeus admin-rooliin vai ei
set Users = {user_1,user_2,user_3}

// Tässä ratkaisussa userista voi tehdä adminin vain kun kyseinen user ei tee customer juttuja.
// Se on huono puoli, mutta voidaan ehkä myös tehdä sellainen päätös että käyttäjän täytyy
// poistua customer/shopkeeper-roolistaan ennen kuin hänelle voidaan antaa admin-rooli.

USER(N=0) = USER[N],
USER[b:BOOL] = (selectCust -> revokeCust -> USER[b]
               |when(b==1) selectAdmin -> revokeAdmin -> USER[b]
               |when(b==0) becomeAdmin -> USER[1]).

CUST = (selectCust -> DO_CUST),
DO_CUST = (doCustStuff -> DO_CUST
          |revokeCust -> CUST).

ADMIN = (selectAdmin -> DO_ADMIN),
DO_ADMIN = (doAdminStuff -> DO_ADMIN
           |Users.makeAdmin -> DO_ADMIN
           |revokeAdmin -> ADMIN).

// Relabling siten että esim. user_1.user_2.makeAdmin == user_2.becomeAdmin
||SYSTEM = (user_1:USER(1) /{Users.user_1.makeAdmin / user_1.becomeAdmin}
          ||user_2:USER(0) /{Users.user_2.makeAdmin / user_2.becomeAdmin}
          ||user_3:USER(0) /{Users.user_3.makeAdmin / user_3.becomeAdmin}
          ||user_1::CUST || user_1::ADMIN
          ||user_2::CUST || user_2::ADMIN
          ||user_3::CUST || user_3::ADMIN).


// Tässä taas on sikäli parempi ratkaisu, että userista voi tehdä adminin myös silloin kun se on
// tekemässä customer juttuja. Eikö olisi hassua jos täytyy odottaa että user poistuu
// customer-roolista ennen kuin hänelle voidaan antaa oikeus admin-rooliin?
// Tässä on vikana se että jostain syystä aina on valittavana vaihotehto, että ollessaan
// admin-roolissa user on valittavana vaihtoehto tehdä itse itsestään uudestaan adminin,
// esim. user_1.user_1.makeAdmin.
// Ei muuten mitään haittaa mutta hölmön näköistä.

USER2(N=0) = USER2[N],
USER2[b:BOOL] = (selectCust -> DO_CUST[b]
                |when(b==1) selectAdmin -> DO_ADMIN[b]
                |when(b==0) becomeAdmin -> USER2[1]),

DO_CUST[b:BOOL] = (doCustStuff -> DO_CUST[b]
                  |revokeCust -> USER2[b]
                  |when(b==0) becomeAdmin -> DO_CUST[1]),

DO_ADMIN[b:BOOL] = (doAdminStuff -> DO_ADMIN[b]
                   |Users.makeAdmin -> DO_ADMIN[b]
                   |revokeAdmin -> DO_ADMIN[b]).

||SYSTEM2 = (user_1:USER2(1) /{{user_2,user_3}.user_1.makeAdmin / user_1.becomeAdmin}
           ||user_2:USER2(0) /{{user_1,user_3}.user_2.makeAdmin / user_2.becomeAdmin}
           ||user_3:USER2(0) /{{user_1,user_2}.user_3.makeAdmin / user_3.becomeAdmin}).
